cmake_minimum_required(VERSION 3.5)

project(mlib VERSION 0.0.2)

set(SOURCES ${CMAKE_CURRENT_SOURCE_DIR}/src/lib.cpp)
set(HEADERS ${CMAKE_CURRENT_SOURCE_DIR}/include/mlib/lib.h)


add_library(mlib_headers INTERFACE)
target_include_directories(mlib_headers INTERFACE
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:include>
)
target_compile_features(mlib_headers INTERFACE cxx_std_14)

include(cmake/CPM.cmake)
CPMAddPackage("gh:IwM341/GridObject@1.0.1")

find_package(Grob)


target_link_libraries(mlib_headers INTERFACE Grob::grob)


add_library(mlib::headers ALIAS mlib_headers)

add_library(mlib ${SOURCES})
target_link_libraries(mlib PUBLIC mlib_headers)

add_library(mlib::mlib ALIAS mlib)



install(DIRECTORY include/mlib DESTINATION include)
install(TARGETS mlib_headers mlib EXPORT mlibConfig)
install(EXPORT mlibConfig NAMESPACE mlib:: DESTINATION share/mlib/cmake)

include(CMakePackageConfigHelpers)
write_basic_package_version_file("${PROJECT_BINARY_DIR}/mlibConfigVersion.cmake"
    VERSION
        ${PROJECT_VERSION}
    COMPATIBILITY
        AnyNewerVersion
)
install(FILES "${PROJECT_BINARY_DIR}/mlibConfigVersion.cmake" DESTINATION share/mlib/cmake)